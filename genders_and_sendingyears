PREFIX bio: <http://purl.org/vocab/bio/0.1/>
PREFIX bioc: <http://ldf.fi/schema/bioc/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX schema: <http://schema.org/>
PREFIX skosxl: <http://www.w3.org/2008/05/skos-xl#>
PREFIX text: <http://jena.apache.org/text#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

PREFIX : <http://ldf.fi/schema/coco/> 
PREFIX actors: <http://ldf.fi/coco/actors/>
PREFIX events: <http://ldf.fi/coco/events/>
PREFIX letters: <http://ldf.fi/coco/letters/>
PREFIX places: <http://ldf.fi/coco/places/>

SELECT ?sendername  ?sender_gender ?receivername  ?receiver_gender ?sendingYear  WHERE {
  ?sub a crm:E21_Person ; #is an instance of persons
  	bioc:has_gender ?gender ; #gender
  	skos:prefLabel ?sendername . #name (=label)
  
  ?production 
    :was_authored_by ?sub; #this person was the sender
    crm:P4_has_time-span ?timespan ; #timespan of one letter
    :letter ?letter .
    
   
  ?letter :was_addressed_to ?receiver.
  ?timespan crm:P82a_begin_of_the_begin ?bTime .
  BIND(year(?bTime) AS ?sendingYear) #extract only the year
  
  
  ?receiver 
    skos:prefLabel ?receivername ;
    bioc:has_gender ?gender2.
    
   ?gender skos:prefLabel ?sender_gender .
   ?gender2 skos:prefLabel ?receiver_gender .
   
} LIMIT 1000



